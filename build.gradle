plugins {
  id "java"
  id "distribution"
  id "com.palantir.docker" version "0.19.2"
}

ext {
  interlokCoreVersion = project.hasProperty('interlokCoreVersion') ? project.getProperty('interlokCoreVersion') : '3.8-SNAPSHOT'
  defaultNexusRepo = project.hasProperty('defaultNexusRepo') ? project.getProperty('defaultNexusRepo') : 'https://repo1.maven.org/maven2/'
  dockerPrefix = System.properties.getProperty('user.name')
}

distTar.enabled=false
distZip.enabled=false
dockerfileZip.enabled=false
compileJava.enabled=false
processResources.enabled=false
classes.enabled=false
jar.enabled=false

repositories {
  maven { url "$defaultNexusRepo" }
  maven { url "https://development.adaptris.net/nexus/content/groups/public" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/snapshots" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/releases" }
}

configurations() {
  war{}
}

dependencies {
  compile ("com.adaptris:interlok-core:$interlokCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-boot:$interlokCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-xinclude:$interlokCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-varsub:$interlokCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-csv:$interlokCoreVersion") { changing=true}
  war ("com.adaptris.ui:interlok:$interlokCoreVersion@war") {
    changing=true
  }
}


distributions {
    main {
        contents {
            into('lib') {
                from(project.configurations.runtime)
            }
            into('webapps') {
                from(project.configurations.war)
            }
            rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
            rename '(.*)-[0-9]+\\..*.war', '$1.war'
        }
    }
}

docker {
    name "$dockerPrefix/docker-interlok-template"
    tags 'latest'
    pull true
    files tasks.installDist.outputs
}

docker.dependsOn installDist
