plugins {
  id "java"
  id "distribution"
  id "com.palantir.docker" version "0.19.2"
}

ext {
  adpCoreVersion = project.hasProperty('adpCoreVersion') ? project.getProperty('adpCoreVersion') : '3.7-SNAPSHOT'
  defaultNexusRepo = project.hasProperty('defaultNexusRepo') ? project.getProperty('defaultNexusRepo') : 'https://repo1.maven.org/maven2/'
  dockerPrefix = System.properties.getProperty('user.name')
}

distTar.enabled=false
distZip.enabled=false
dockerfileZip.enabled=false
compileJava.enabled=false
processResources.enabled=false
classes.enabled=false
jar.enabled=false

repositories {
  maven { url "$defaultNexusRepo" }
  maven { url "https://development.adaptris.net/nexus/content/groups/public" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/snapshots" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/releases" }
}

configurations() {
  war{}
}

dependencies {
  compile ("com.adaptris:adp-core:$adpCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-boot:$adpCoreVersion") { changing= true}
  compile ("com.adaptris:adp-xinclude:$adpCoreVersion") { changing= true}
  compile ("com.adaptris:adp-varsub:$adpCoreVersion") { changing= true}
  compile ("com.adaptris:adp-simple-csv:$adpCoreVersion") { changing=true}
  war ("com.adaptris.ui:interlok:$adpCoreVersion@war") {
    changing=true
  }
}


distributions {
    main {
        contents {
            into('lib') {
                from(project.configurations.runtime)
            }
            into('webapps') {
                from(project.configurations.war)
            }
            rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
            rename '(.*)-[0-9]+\\..*.war', '$1.war'
        }
    }
}

docker {
    name "$dockerPrefix/docker-interlok-template"
    tags 'latest'
    pull true
    files tasks.installDist.outputs
}

docker.dependsOn installDist
